#include <ESP8266WiFi.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <ESP8266WebServer.h>
#include <NTPClient.h>
#include <WiFiUdp.h>

// 设置 OLED 屏幕的宽度和高度
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64

// 设置 NTP 服务器和时区
WiFiUDP ntpUDP;
NTPClient timeClient(ntpUDP, "pool.ntp.org", 3600 * 8);

// 定义输出引脚
const int relay1Pin = 0; // D3
const int relay2Pin = 2; // D4

// 定义 OLED 的 I2C 引脚
const int sclPin = 1;
const int sdaPin = 3;

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

ESP8266WebServer server(80);

// 定义一个结构体来存储时间信息
struct TimeInfo {
    int hour;
    int minute;
    int second;
    int day;
    int month;
    int year;
};

// 存储定时的开始小时和分钟
int startHourForGroup1 = 0;
int startMinuteForGroup1 = 0;
// 存储定时的结束小时和分钟
int endHourForGroup1 = 0;
int endMinuteForGroup1 = 0;
// 存储第二个定时的开始小时和分钟
int startHourForGroup2 = 0;
int startMinuteForGroup2 = 0;
// 存储第二个定时的结束小时和分钟
int endHourForGroup2 = 0;
int endMinuteForGroup2 = 0;

// 标志位，用于判断是否设置了定时
bool isTimed = false;

// 存储 IP 地址的字符串
String ipAddressStr;

// 用于滚动的字符串
String scrollText = "You have more beauty to achieve. yaoyang production";
int scrollPosition = 0;

// 登录状态标志
bool isLoggedIn = false;

// 定义管理员用户名和密码
const char* adminUsername = "admin";
const char* adminPassword = "password";

// 获取当前时间并返回结构体的函数
TimeInfo getCurrentTime() {
    time_t epochTime = timeClient.getEpochTime();
    struct tm *timeInfo;
    timeInfo = localtime(&epochTime);
    TimeInfo currentTime;
    currentTime.hour = timeInfo->tm_hour;
    currentTime.minute = timeInfo->tm_min;
    currentTime.second = timeInfo->tm_sec;
    currentTime.day = timeInfo->tm_mday;
    currentTime.month = timeInfo->tm_mon + 1; // tm_mon 从 0 开始
    currentTime.year = timeInfo->tm_year + 1900;
    return currentTime;
}

void handleRoot() {
    if (!isLoggedIn) {
        server.sendHeader("Location", "/login");
        server.send(303);
        return;
    }

    String html = "<html><head><title>Relay Control</title></head><body>"
                  "<p><a href=\"/relay1?state=1\">Turn on Relay 1</a></p>"
                  "<p><a href=\"/relay1?state=0\">Turn off Relay 1</a></p>"
                  "<p><a href=\"/relay2?state=1\">Turn on Relay 2</a></p>"
                  "<p><a href=\"/relay2?state=0\">Turn off Relay 2</a></p>"
                  "<p><a href=\"/setTime\">Set Timing</a></p>"
                  "<p><a href=\"/cancelTiming\">Cancel Timing</a></p>"
                  "</body></html>";
    server.send(200, "text/html", html);
}

void handleSetTime() {
    if (!isLoggedIn) {
        server.sendHeader("Location", "/login");
        server.send(303);
        return;
    }

    if (server.method() == HTTP_POST) {
        Serial.println("Received POST request for setting time.");
        startHourForGroup1 = server.arg("startHourForGroup1").toInt();
        startMinuteForGroup1 = server.arg("startMinuteForGroup1").toInt();
        endHourForGroup1 = server.arg("endHourForGroup1").toInt();
        endMinuteForGroup1 = server.arg("endMinuteForGroup1").toInt();
        startHourForGroup2 = server.arg("startHourForGroup2").toInt();
        startMinuteForGroup2 = server.arg("startMinuteForGroup2").toInt();
        endHourForGroup2 = server.arg("endHourForGroup2").toInt();
        endMinuteForGroup2 = server.arg("endMinuteForGroup2").toInt();
        isTimed = true;
        Serial.println("Set startHourForGroup1: " + String(startHourForGroup1));
        Serial.println("Set startMinuteForGroup1: " + String(startMinuteForGroup1));
        Serial.println("Set endHourForGroup1: " + String(endHourForGroup1));
        Serial.println("Set endMinuteForGroup1: " + String(endMinuteForGroup1));
        Serial.println("Set startHourForGroup2: " + String(startHourForGroup2));
        Serial.println("Set startMinuteForGroup2: " + String(startMinuteForGroup2));
        Serial.println("Set endHourForGroup2: " + String(endHourForGroup2));
        Serial.println("Set endMinuteForGroup2: " + String(endMinuteForGroup2));
        server.send(200, "text/html; charset=utf-8", "设置成功！");
    } else if (server.method() == HTTP_GET) {
        String html = "<html><head><meta charset=\"UTF-8\"></head><body>";
        html += "<h1>设置定时时间</h1>";
        html += "<form method='post' action='/setTime'>";

        // 第一组定时设置
        html += "<h2>第一组定时设置</h2>";
        html += "开启时间：<br>";
        html += "小时：<input type='number' name='startHourForGroup1' min='0' max='23' value='" + String(startHourForGroup1) + "'>";
        html += "分钟：<input type='number' name='startMinuteForGroup1' min='0' max='59' value='" + String(startMinuteForGroup1) + "'><br><br>";
        html += "关闭时间：<br>";
        html += "小时：<input type='number' name='endHourForGroup1' min='0' max='23' value='" + String(endHourForGroup1) + "'>";
        html += "分钟：<input type='number' name='endMinuteForGroup1' min='0' max='59' value='" + String(endMinuteForGroup1) + "'><br><br>";

        // 第二组定时设置
        html += "<h2>第二组定时设置</h2>";
        html += "开启时间：<br>";
        html += "小时：<input type='number' name='startHourForGroup2' min='0' max='23' value='" + String(startHourForGroup2) + "'>";
        html += "分钟：<input type='number' name='startMinuteForGroup2' min='0' max='59' value='" + String(startMinuteForGroup2) + "'><br><br>";
        html += "关闭时间：<br>";
        html += "小时：<input type='number' name='endHourForGroup2' min='0' max='23' value='" + String(endHourForGroup2) + "'>";
        html += "分钟：<input type='number' name='endMinuteForGroup2' min='0' max='59' value='" + String(endMinuteForGroup2) + "'><br><br>";

        html += "<input type='submit' value='设置定时'>";
        html += "</form>";
        html += "</body></html>";
        server.send(200, "text/html; charset=utf-8", html);
    }
}

void handleCancelTiming() {
    if (!isLoggedIn) {
        server.sendHeader("Location", "/login");
        server.send(303);
        return;
    }

    isTimed = false;
    server.send(200, "text/html; charset=utf-8", "定时已取消！");
}

void handleRelay1() {
    if (!isLoggedIn) {
        server.sendHeader("Location", "/login");
        server.send(303);
        return;
    }

    String state = server.arg("state");
    if (state == "1") {
        digitalWrite(relay1Pin, HIGH);  // 打开继电器1
        Serial.println("Relay 1 turned ON");
    } else if (state == "0") {
        digitalWrite(relay1Pin, LOW);  // 关闭继电器1
        Serial.println("Relay 1 turned OFF");
    }
    server.send(200, "text/plain", "Relay 1 state changed");
}

void handleRelay2() {
    if (!isLoggedIn) {
        server.sendHeader("Location", "/login");
        server.send(303);
        return;
    }

    String state = server.arg("state");
    if (state == "1") {
        digitalWrite(relay2Pin, HIGH);  // 打开继电器2
        Serial.println("Relay 2 turned ON");
    } else if (state == "0") {
        digitalWrite(relay2Pin, LOW);  // 关闭继电器2
        Serial.println("Relay 2 turned OFF");
    }
    server.send(200, "text/plain", "Relay 2 state changed");
}

void handleLogin() {
    if (server.method() == HTTP_GET) {
        String html = "<html><head><title>Login</title><style>"
                      "body { font-family: Arial, sans-serif; text-align: center; }"
                      "form { margin-top: 50px; }"
                      "input { padding: 10px; margin: 10px; }"
                      "button { padding: 10px 20px; }"
                      "</style></head><body>"
                      "<h1>Login</h1>"
                      "<form method='post' action='/login'>"
                      "Username: <input type='text' name='username'><br>"
                      "Password: <input type='password' name='password'><br>"
                      "<button type='submit'>Login</button>"
                      "</form>"
                      "</body></html>";
        server.send(200, "text/html", html);
    } else if (server.method() == HTTP_POST) {
        String username = server.arg("username");
        String password = server.arg("password");

        if (username == adminUsername && password == adminPassword) {
            isLoggedIn = true;
            server.sendHeader("Location", "/");
            server.send(303);
        } else {
            server.send(401, "text/html", "Invalid username or password");
        }
    }
}

void updateOLED() {
    display.clearDisplay();
    
    // 固定显示日期时间在最上方
    TimeInfo currentTime = getCurrentTime();
    display.setCursor(0, 0);
    
    display.print(currentTime.day);
    display.print("/");
    display.print(currentTime.month);
    display.print("/");
    display.print(currentTime.year);
    display.setCursor(80, 0);
    
    display.print(currentTime.hour);
    display.print(":");
    display.print(currentTime.minute);
    display.print(":");
    display.print(currentTime.second);

    // 固定显示 IP 地址
    display.setCursor(0, 10);
    display.println(ipAddressStr);

    // 显示继电器状态
    display.setCursor(0, 20);
    display.print(digitalRead(relay1Pin) ? "Relay 1: ON  " : "Relay 1: OFF ");
    display.setCursor(0, 30);
    display.print(digitalRead(relay2Pin) ? "Relay 2: ON" : "Relay 2: OFF");

    // 显示定时设置
    if (isTimed) {
        display.setCursor(0, 40);
        display.print("time 1: ");
        display.print(startHourForGroup1);
        display.print(":");
        display.print(startMinuteForGroup1);
        display.print(" - ");
        display.print(endHourForGroup1);
        display.print(":");
        display.print(endMinuteForGroup1);

        display.setCursor(0, 50);
        display.print("time 2: ");
        display.print(startHourForGroup2);
        display.print(":");
        display.print(startMinuteForGroup2);
        display.print(" - ");
        display.print(endHourForGroup2);
        display.print(":");
        display.print(endMinuteForGroup2);
    } else {
        display.setCursor(0, 40);
        display.println("No timing set");
    
        display.setCursor(0, 50);
        display.println("Timer switch V2.6.3");
    }

    // 倒数第二行显示滚动的底部文本
    display.setCursor(0, SCREEN_HEIGHT - 8);
    display.print(scrollText.substring(scrollPosition));
    scrollPosition++;
    if (scrollPosition >= scrollText.length()) {
        scrollPosition = 0;
    }

    display.display();
}

void setup() {
    Serial.begin(115200);
    WiFi.begin("2.4G", "8888888888");
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.println("连接 WiFi 中...");
    }
    Serial.println("已连接到 WiFi");

    // 初始化输出引脚为低电平
    pinMode(relay1Pin, OUTPUT);
    digitalWrite(relay1Pin, LOW);
    pinMode(relay2Pin, OUTPUT);
    digitalWrite(relay2Pin, LOW);

    // 设置 I2C 引脚
    Wire.begin(sdaPin, sclPin);

    // 初始化 OLED 屏幕
    if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
        Serial.println(F("SSD1306 分配失败"));
        for (;;);
    }
    
    display.setTextSize(1);
    display.setTextColor(WHITE);
    display.setCursor(0, 0);
    display.println("Loading");
    display.display();
    delay(2000); // 暂停2秒
    display.clearDisplay(); 

    // 初始化 NTP 客户端
    timeClient.begin();

    server.on("/", handleRoot);
    server.on("/setTime", handleSetTime);
    server.on("/cancelTiming", handleCancelTiming);
    server.on("/relay1", handleRelay1);
    server.on("/relay2", handleRelay2);
    server.on("/login", handleLogin);
    server.begin();

    // 获取并保存 IP 地址
    IPAddress localIP = WiFi.localIP();
    ipAddressStr = "IP:" + String(localIP[0]) + "." +
                   String(localIP[1]) + "." +
                   String(localIP[2]) + "." +
                   String(localIP[3]);

    // 确保时间同步
    timeClient.update();
    TimeInfo currentTime = getCurrentTime();
    Serial.print("Current time: ");
    Serial.print(currentTime.hour);
    Serial.print(":");
    Serial.print(currentTime.minute);
    Serial.print(":");
    Serial.println(currentTime.second);
}

void loop() {
    server.handleClient();

    // 更新时间
    timeClient.update();
    TimeInfo currentTime = getCurrentTime();
    Serial.print("Current time: ");
    Serial.print(currentTime.hour);
    Serial.print(":");
    Serial.print(currentTime.minute);
    Serial.print(":");
    Serial.println(currentTime.second);

    // 如果设置了定时，判断是否在定时时间段内
    if (isTimed) {
        bool isInOnPeriodForGroup1 = isInOnPeriod(startHourForGroup1, startMinuteForGroup1, endHourForGroup1, endMinuteForGroup1);
        bool isInOnPeriodForGroup2 = isInOnPeriod(startHourForGroup2, startMinuteForGroup2, endHourForGroup2, endMinuteForGroup2);

        // 根据状态设置输出引脚
        if (isInOnPeriodForGroup1) {
            digitalWrite(relay1Pin, HIGH);  // 打开继电器1
            digitalWrite(relay2Pin, LOW);   // 关闭继电器2
            Serial.println("Group 1 is ON");
        } else if (isInOnPeriodForGroup2) {
            digitalWrite(relay1Pin, LOW);   // 关闭继电器1
            digitalWrite(relay2Pin, HIGH);  // 打开继电器2
            Serial.println("Group 2 is ON");
        } else {
            digitalWrite(relay1Pin, LOW);   // 关闭继电器1
            digitalWrite(relay2Pin, LOW);   // 关闭继电器2
            Serial.println("Both groups are OFF");
        }
    }

    updateOLED();
    delay(1000);
}

// 判断当前时间是否在指定的时间段内
bool isInOnPeriod(int startHour, int startMinute, int endHour, int endMinute) {
    TimeInfo currentTime = getCurrentTime();
    int currentTotalMinutes = currentTime.hour * 60 + currentTime.minute;
    int startTotalMinutes = startHour * 60 + startMinute;
    int endTotalMinutes = endHour * 60 + endMinute;
    Serial.print("currentTotalMinutes: ");
    Serial.println(currentTotalMinutes);
    Serial.print("startTotalMinutes: ");
    Serial.println(startTotalMinutes);
    Serial.print("endTotalMinutes: ");
    Serial.println(endTotalMinutes);
    if (endTotalMinutes < startTotalMinutes) {
        // 跨越到第二天的情况
        return currentTotalMinutes >= startTotalMinutes || currentTotalMinutes < endTotalMinutes;
    } else {
        // 同一天的情况
        return currentTotalMinutes >= startTotalMinutes && currentTotalMinutes < endTotalMinutes;
    }
}
